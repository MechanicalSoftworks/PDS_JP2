#!/bin/csh
#
#	CVS_ID_Check
#
#	Use:
#
#		CVS_ID_Check [-Directory <directory>] <subsystem>
#
#	Description:
#
#	If a -directory is not specified the parent
#	of the current directory will be used as the root directory for
#	locating subsystem subdirectories.
#
#	Author:
#
#		Bradford Castalia, UA/PIRL/HiROC
#
#	CVS ID: CVS_ID_Check,v 1.3 2008/02/15 00:27:42 castalia Exp

set procedure = `basename $0`

while ($#argv)
	switch ($argv[1])
		case -[Dd]*:
			shift
			if ($?Directory) then
				echo "${procedure}: Multiple directory pathnames specified -"
				echo "  $Directory"
				echo "  $argv[1]"
				goto Usage
			endif
			if (! $#argv) then
				echo "${procedure}: Missing directory pathname."
				goto Usage
			endif
			set Directory = $argv[1]
			breaksw
		case -[Vv]*:
			set Verbose = 1
			breaksw
		case -[Hh]*:
			Usage:
			echo "Usage: $0 [-Verbose] [-Directory <directory>] <subsystem>"
			echo "  Default directory: .."
			exit 1
			breaksw
		case -*:
			echo "${procedure}: Unrecognized $argv[1] option."
			goto Usage
			breaksw
		default:
			if ($?Subsystem) then
				echo "${procedure}: Multiple subsystems specified -"
				echo "  $Subsystem"
				echo "  $argv[1]"
				goto Usage
			endif
			set Subsystem = $argv[1]
			breaksw
	endsw
	shift
end

if (! $?Subsystem) then
	echo "${procedure}: No subsystem specified."
	goto Usage
endif
if (! -d /HiRISE/$Subsystem) then
	echo "${procedure}: No such subsystem: $Subsystem"
	exit 1
endif

if (! $?Directory) then
	set Directory = `pwd`
	set Directory = $Directory/..
endif
if (! -d $Directory) then
	echo "${procedure}: No such directory: $Directory"
	exit 1
endif

(cd /HiRISE/$Subsystem; \
find . \( -name docs -prune \) -o -type f \
	-exec grep -q CVS \{\} \; -print \
	-exec grep CVS \{\} \; \
	-exec grep CVS $Directory/$Subsystem/\{\} \;)

exit 0
