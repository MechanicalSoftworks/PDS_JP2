#!/bin/csh
#
#	Versions
#
#	Use:
#
#		Versions [-All] [<directory>]
#
#	Description:
#
#	The version number from the VERSION file in each subdirectory of the
#	specified directory is listed along with its subdirectory name (which
#	is expected to be a subsystem name). If no directory is specified,
#	the value of $HiRISE_ROOT is used; if no HiRISE_ROOT variable is
#	defined, the parent directory is used. If $SUBSYSTEMS is defined, its
#	value is used to select subdirectories to examine.
#
#	A subdirectory with a VERSION file has the version number extracted.
#	The version number is on a line beginning with a decimal digit; all
#	other lines in the file are ignored. The version number followed by a
#	tab character and the subdirectory name is listed.
#
#	-All
#
#	Normally a subdirectory that does not exist - it is in the SUBSYSTEMS
#	list - or does not contain a VERSION file is simply ignored. If the
#	-All option is specified these cases will be noted: If a selected
#	subdirectory does not exist the version number in the listing is
#	replaced with "---". If a subdirectory does not contain a VERSION
#	file, the version number in the listing is replaced with "???".
#
#	Author:
#
#		Bradford Castalia, UA/PIRL/HiROC
#
#	CVS ID: Versions,v 1.3 2007/07/07 00:06:15 castalia Exp

while ($#argv)
	switch ($argv[1])
		case -[Aa]*:
			set Report_All = 1
			breaksw
		case -[Hh]*:
			Usage:
			echo "Usage: $0 [-All] [<directory>]"
			exit 1
			breaksw
		case -*:
			echo "Unrecognized option: $argv[1]"
			goto Usage
			breaksw
		default:
			if ($?Directory) then
				echo "Multiple directories specified:"
				echo "  $Directory"
				echo "  $argv[1]"
				goto Usage
			endif
			set Directory = $argv[1]
			breaksw
	endsw
	shift
end

if (! $?Directory) then
	if ($?HiRISE_ROOT) then
		set Directory = $HiRISE_ROOT
	else
		set Directory = ".."
	endif
endif
if (! -d $Directory) then
	echo "No such directory: $Directory"
	exit 1
endif
echo "Versions in subdirectories of ${Directory}:"

if ($?SUBSYSTEMS) then
	echo "Using SUBSYSTEMS list."
	set Selections = ()
	foreach subsystem ($SUBSYSTEMS)
		set Selections = ($Selections $Directory/$subsystem)
	end
else
	set Selections = ($Directory/*)
endif

foreach selection ($Selections)
	if (! -d $selection) then
		if ($?Report_All && ! -e $selection) echo "---	$selection:t"
		continue
	endif
	if (-f $selection/VERSION) then
		set version = (`grep '^[0-9]' $selection/VERSION`)
		echo "$version	$selection:t"
	else if ($?Report_All) then
		echo "???	$selection:t"
	endif
end
exit 0
